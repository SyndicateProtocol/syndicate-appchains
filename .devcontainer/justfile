# Add Foundry's bin directory to the PATH for all recipes
export PATH := env_var('PATH') + ":" + env_var('HOME') + "/.foundry/bin"

# Clone the Optimism repository
clone:
    git clone https://github.com/ethereum-optimism/optimism.git ~/optimism

# Install Foundry
# The install can sometimes fail on the first attempt, so we try twice
# Yes, this is what the official Optimism devnet docs recommend doing:
# https://docs.optimism.io/chain/testing/dev-node#install-foundry-using-monorepo-tooling
install-foundry:
    #!/bin/bash
    set -e
    cd ~/optimism
    if ! just install-foundry; then
        echo "First install-foundry attempt failed. Sourcing .bashrc and trying again."
        source ~/.bashrc
        just install-foundry
    else
        echo "install-foundry succeeded on first attempt."
    fi
    source ~/.bashrc

# Initialize op-devnet
op-devnet-up:
    make --directory ~/optimism op-devnet-up
    @echo "OP Devnet initialized"

# Shut down devnet
op-devnet-down:
    make --directory ~/optimism op-devnet-down
    @echo "OP Devnet shut down"

# Create aliases for devnet commands in both Bash and Zsh
create-aliases:
    #!/bin/bash
    for rc_file in ~/.bashrc ~/.zshrc; do
        if [[ -f "$rc_file" ]]; then
            echo "# Metabased DevNet aliases" >> "$rc_file"
            echo "alias op-up='just -f ~/metabased-devnet.justfile op-devnet-up'" >> "$rc_file"
            echo "alias op-down='just -f ~/metabased-devnet.justfile op-devnet-down'" >> "$rc_file"
            echo "Aliases created in $rc_file"
        else
            echo "Warning: $rc_file does not exist. Skipping."
        fi
    done

# Run all steps in sequence
# OP Devnet setup based on https://docs.optimism.io/chain/testing/dev-node
# We initialize and then spin down the devnet to get the initialization time out
# of the way upfront
all: clone install-foundry op-devnet-up op-devnet-down copy-justfile create-aliases
    @echo "Post-create script completed successfully"

# Copy Justfile to ~/metabased-devnet.justfile
# This gives easy access to op-devnet commands after the container is created
copy-justfile:
    cp {{justfile()}} ~/metabased-devnet.justfile
    @echo "Justfile copied to ~/metabased-devnet.justfile"
