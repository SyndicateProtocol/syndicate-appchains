# Add Foundry's bin directory to the PATH for all recipes
export PATH := env_var('PATH') + ":" + env_var('HOME') + "/.foundry/bin"

# Clone the Optimism repository
op-clone:
    git clone https://github.com/ethereum-optimism/optimism.git ~/optimism

# Install Foundry
# Based on https://book.getfoundry.sh/getting-started/installation
foundry-install:
    curl -L https://foundry.paradigm.xyz | bash

# Update Foundry
# Based on https://book.getfoundry.sh/getting-started/installation
foundry-update:
    foundryup

# Initialize op-devnet
op-devnet-up:
    make --directory ~/optimism devnet-up
    @echo "OP Devnet initialized"

# Shut down devnet
op-devnet-down:
    make --directory ~/optimism devnet-down
    @echo "OP Devnet shut down"

# Create aliases for devnet commands in both Bash and Zsh
create-aliases:
    #!/bin/bash
    for rc_file in ~/.bashrc ~/.zshrc; do
        if [[ -f "$rc_file" ]]; then
            echo "# Foundry aliases" >> "$rc_file"
            echo "alias foundry-install='just -f {{justfile()}} foundry-install'" >> "$rc_file"
            echo "alias foundry-update='just -f {{justfile()}} foundry-update'" >> "$rc_file"
            echo "# Metabased DevNet aliases" >> "$rc_file"
            echo "alias op-up='just -f {{justfile()}} op-devnet-up'" >> "$rc_file"
            echo "alias op-down='just -f {{justfile()}} op-devnet-down'" >> "$rc_file"
            echo "Aliases created in $rc_file"
        else
            echo "Warning: $rc_file does not exist. Skipping."
        fi
    done

# Run all steps in sequence
# OP Devnet setup based on https://docs.optimism.io/chain/testing/dev-node
# We initialize and then spin down the devnet to get the initialization time out
# of the way upfront
all: op-clone foundry-install op-devnet-up op-devnet-down create-aliases
    @echo "Post-create script completed successfully"
